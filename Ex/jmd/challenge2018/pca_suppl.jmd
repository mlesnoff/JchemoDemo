---
title: challenge2018 - Pca with supplementary observations
weave_options:
  error: true
  wrap: true
  term: false
  #out_width: "50%"   # default
---

```julia
using Jchemo, JchemoData
using JLD2, CairoMakie
using FreqTables
```

#### Data importation

```julia
path_jdat = dirname(dirname(pathof(JchemoData)))
db = joinpath(path_jdat, "data/challenge2018.jld2") 
@load db dat
pnames(dat)
```

#### Data preparation and short description

```julia
X = dat.X 
Y = dat.Y
ntot, p = size(X)
```

```julia term = true
@head X
@head Y
```

```julia
summ(Y)
```

```julia
y = Y.conc
typ = Y.typ
label = Y.label 
test = Y.test
tab(test)
```

```julia
wlst = names(X)
wl = parse.(Float64, wlst) 
```

```julia
freqtable(string.(typ, "-", Y.label))
```

```julia
freqtable(typ, test)
```

```julia
plotsp(X, wl; nsamp = 30).f
```

#### Preprocessing

```julia
model1 = snv()
model2 = savgol(npoint = 21, deriv = 2, degree = 3)
model = pip(model1, model2)
fit!(model, X)
Xp = transf(model, X) 
```

```julia
plotsp(Xp, wl; nsamp = 30).f
```

#### Split Tot ==> Train + Test

The model is tuned on **Train**. Here the split of **Tot** is already provided inside 
the dataset (= variable `test`), but **Tot** could also be split *a posteriori*, for instance 
by sampling (random, systematic or any other designs). 

```julia
s = Bool.(test)
Xtrain = rmrow(Xp, s)
Ytrain = rmrow(Y, s)
ytrain = rmrow(y, s)
typtrain = rmrow(typ, s)
Xtest = Xp[s, :]
Ytest = Y[s, :]
ytest = y[s]
typtest = typ[s]
ntrain = nro(Xtrain)
ntest = nro(Xtest)
(ntot = ntot, ntrain, ntest)
```

#### Pca on Xtrain

```julia
nlv = 15
model = pcasvd(; nlv)
fit!(model, Xtrain)
```

```julia
res = summary(model, Xtrain).explvarx
```

```julia
plotgrid(res.nlv, res.pvar; step = 2, xlabel = "PC", ylabel = "Prop. variance explained").f
```

```julia term = true
@head Ttrain = model.fitm.T
```
#### Projection of Xtest

Below function `transform` has to be qualified since both packages Jchemo 
and DataFrames export a function `transform`. This will be the same with 
common function names such as `predict`, `coef`, etc.

```julia term = true
@head Ttest = transf(model, Xtest)
```

```julia
T = vcat(Ttrain, Ttest)
group = vcat(repeat(["0-Train";], ntrain), repeat(["1-Test";], ntest))
colm = [:blue, (:red, .5)]
i = 3
plotxy(T[:, i], T[:, i + 1], group; color = colm, xlabel = string("PC", i), 
    ylabel = string("PC", i + 1)).f
```

#### SD and OD distances

```julia
model_d = occsdod() 
fit!(model_d, model.fitm, Xtrain)
pnames(model_d)
pnames(model_d.fitm)
```

```julia
dtrain = model_d.fitm.d
```

**Values for Xtest**

```julia
dtest = predict(model_d, Xtest).d
```

```julia
f = Figure(size = (500, 400))
ax = Axis(f[1, 1]; xlabel = "SD", ylabel = "OD")
scatter!(ax, dtrain.dstand_sd, dtrain.dstand_od; label = "Train")
scatter!(ax, dtest.dstand_sd, dtest.dstand_od; color = (:red, .5), label = "Test")
hlines!(ax, 1; color = :grey, linestyle = :dash)
vlines!(ax, 1; color = :grey, linestyle = :dash)
axislegend(position = :rt)
f
```

**Same using plotxy:**

```julia
d = vcat(dtrain, dtest)
group = vcat(repeat(["0-Train";], ntrain), repeat(["1-Test";], ntest))
colm = [:blue, (:red, .5)]
plotxy(d.dstand_sd, d.dstand_od, group; color = colm, xlabel = "Stand. SD", ylabel = "Stand. OD").f
```

#### Composite distance SD-OD

```julia
f = Figure(size = (500, 400))
ax = Axis(f[1, 1]; xlabel = "Standardized distance", ylabel = "Nb. observations")
hist!(ax, dtrain.dstand; bins = 50, label = "Train")
hist!(ax, dtest.dstand; bins = 50, label = "Test")
vlines!(ax, 1; color = :grey, linestyle = :dash)
axislegend(position = :rt, framevisible = false)
f
```
