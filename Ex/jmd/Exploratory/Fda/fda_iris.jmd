---
title: Fda - iris
weave_options:
  error: true
  wrap: false
  term: false
  out_width: "60%" 
---

```julia
using Jchemo, JchemoData
using JLD2, CairoMakie
```

#### Data importation

```julia
path_jdat = dirname(dirname(pathof(JchemoData)))
db = joinpath(path_jdat, "data", "iris.jld2") 
@load db dat
@names dat
```

```julia
summ(dat.X)
```

```julia
@head X = dat.X[:, 1:4] 
```

```julia
@head y = dat.X[:, 5]    # the classes (species)
```
  
```julia
tab(y)
```

```julia
lev = mlev(y)
nlev = length(lev)
```

#### Split Tot to Train/Test

```julia
ntot = nro(X)
ntest = 30
s = samprand(ntot, ntest)
Xtrain = X[s.train, :]
ytrain = y[s.train]
Xtest = X[s.test, :]
ytest = y[s.test]
ntrain = ntot - ntest
(ntot = ntot, ntrain, ntest)
```

```julia
tab(ytrain)
```

```julia
tab(ytest)
```

#### Model fitting

```julia
model = fda(nlv = 2)
#model = fdasvd(nlv = 2)     # alternative algorithm (same result)
fit!(model, Xtrain, ytrain) 
fitm = model.fitm
@names fitm
```

```julia
lev = fitm.lev
nlev = length(lev)
```

**Fda scores**

```julia
@head Ttrain = fitm.T
```

**Class centers projected on the score space**

```julia
ct = fitm.Tcenters
```

```julia
f, ax = plotxy(Ttrain[:, 1], Ttrain[:, 2], ytrain; title = "Fda", ellipse = true)
scatter!(ax, ct[:, 1], ct[:, 2]; markersize = 10, color = :red)
f
```

```julia
color = cgrad(:lightrainbow, nlev; categorical = true, alpha = .7)
f, ax = plotxy(Ttrain[:, 1], Ttrain[:, 2], ytrain; color, title = "Fda", ellipse = true)
scatter!(ax, ct[:, 1], ct[:, 2]; markersize = 10, color = :red)
f
```

#### X-loadings matrix

Columns of matrix `V` are coefficients of the linear discriminant function, corresponding to object `LD` of function 
`lda` of the R package [`MASS`](https://cran.r-project.org/web/packages/MASS/index.html).

```julia
V = fitm.V
```

```julia
V' * V    # not orthogonal
```

#### Explained variance 

```julia
fitm.eig
```

```julia
fitm.sstot
```

Explained variance is computed from the Pca of the class centers in the transformed scale.

```julia
summary(model)
```

#### Projection of Xtest to the score space

```julia
@head Ttest = transf(model, Xtest)
```

```julia
i = 1  # class "i" in test
color = cgrad(:lightrainbow, nlev; categorical = true, alpha = .7)
f, ax = plotxy(Ttrain[:, 1], Ttrain[:, 2], ytrain; color, title = title = "Projection test-class $(lev[i]) (blue points)", 
    ellipse = true)
scatter!(ax, ct[:, 1], ct[:, 2]; markersize = 10, color = :red)
s = ytest .== lev[i]
zT = Ttest[s, :]
scatter!(ax, zT[:, 1], zT[:, 2]; markersize = 10, color = :blue)
f
```



