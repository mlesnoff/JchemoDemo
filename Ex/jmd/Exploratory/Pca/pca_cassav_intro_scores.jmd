---
title: Pca - cassav - introduction - scores 
weave_options:
  error: true
  wrap: true
  term: false
  out_width: 60%
  out_height: 30%
---

```julia term = true
using Jchemo, JchemoData
using JLD2, CairoMakie, GLMakie
```

#### Data importation

```julia term = true
using JchemoData, JLD2, CairoMakie, GLMakie
path_jdat = dirname(dirname(pathof(JchemoData)))
db = joinpath(path_jdat, "data/cassav.jld2")
@load db dat
@names dat
```

```julia term = true
X = dat.X
names(X)
```

#### Model fitting

```julia term = true
model = pcasvd(nlv = 10)
fit!(model, X)  
@names model
```

```julia term = true
fitm = model.fitm ; 
@names fitm
```

#### Scores T

The scores are already computed and returned in sub-object `fitm` 

```julia term = true
@head T = fitm.T 
```

but can also be computed by projecting the data on the loadings
(more generally, this projection is done to compute the scores of new data not used in the model fitting)

```julia term = true
@head transf(model, X) 
```

#### % Variance explained

```julia term = true
res = summary(model, X) ;
@names res
```

```julia term = true
pcts = res.explvarx
```

```julia term = true
CairoMakie.activate!()
plotgrid(pcts.nlv, 100 * pcts.pvar; step = 1, xlabel = "nb. PCs", ylabel = "% variance explained").f
```

#### 2-D score spaces 

For instance, the space PC1-PC2 can be plotted by

```julia term = true
plotxy(T[:, 1], T[:, 2]; zeros = true, xlabel = "PC1", ylabel = "PC2").f
```

More information (recommended in practice) can be displayed using function `plotlv`

```julia term = true
plotlv(T[:, 1:6]; shape = (2, 3), color = (:blue, .5), zeros = true, xlabel = "PC", 
    ylabel = "PC").f
```

See functions `plotxy` and `plotlv` for more options.

#### 3-D score spaces

3D representations of the score space are often more informative than 2D representations,
especially when using interactive axe-rotation.

```julia term = true
CairoMakie.activate!()  
#GLMakie.activate!()    # for interactive axe-rotation

i = 1
size = (600, 350)
plotxyz(T[:, i], T[:, i + 1], T[:, i + 2]; size, color = (:red, .3), markersize = 10, 
    xlabel = string("PC", i), ylabel = string("PC", i + 1), zlabel = string("PC", i + 2), 
    title = "Pca score space").f
```

See function `plotxyz` for more options.

